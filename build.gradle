buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}

apply plugin: 'com.android.application'

// Call gradle with -Plint to enable all warnings.
if (hasProperty("lint")) {
    tasks.withType(Compile) {
        options.compilerArgs << "-Xlint:all"
    }
}

android {

    buildToolsVersion "22.0.1"
    compileSdkVersion 22

    defaultConfig {
        versionName '1.0.5'
        versionCode 13
        minSdkVersion 14
        targetSdkVersion 22
        applicationId 'nl.mpcjanssen.uc'
    }

    signingConfigs {
        release {
            keyAlias = "mpcjanssen.nl"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
}

// Only sign if we have keystore properties in the gradle.properties in ~/.gradle
if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
